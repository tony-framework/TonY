plugins {
  id "com.commercehub.gradle.plugin.avro" version "0.9.0"
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

dependencies {
  compile(deps.azkaban.az_core) { transitive = false }
  compile(deps.azkaban.az_hadoop_jobtype_plugin) { transitive = false }
  compile(deps.azkaban.azkaban_common) { transitive = false }
  compile(deps.external.avro) {
    exclude group: 'org.slf4j'
  }
  compile deps.external.httpclient
  compile deps.external.jackson_databind
  compile deps.external.py4j
  compile deps.external.text
  compile deps.external.zip4j
  compile(deps.hadoop.common) {
    exclude group: 'org.slf4j'
  }
  compile deps.hadoop.hdfs
  compile deps.hadoop.yarn_api
  compile deps.hadoop.yarn_client
  compile deps.hadoop.yarn_common

  testCompile project(':tony-mini')
  testCompile deps.external.avro
  testCompile deps.external.testng
  testCompile deps.external.mockito

  // Only needed by Hadoop test classes
  testRuntime deps.external.junit
  testRuntime deps.external.slf4j_api
}

task setupHdfsLib(type: Copy) {
  from jar, configurations.compile
  exclude '**/*.pom'
  into "$rootDir/tony-core/out/libs/"
}

test {
  workingDir = rootDir
}

test.dependsOn(setupHdfsLib)

def generatedSourcesDir = file('src/generated/main/java')
sourceSets {
  main {
    proto {
      // In addition to the default 'src/main/proto'
      srcDir 'src/main/proto'
      include '**/*.protodevel'
    }

    java {
      srcDirs += [generatedSourcesDir]
    }
  }
}

protobuf {
  // Configure the protoc executable
  protoc {
    // Download from repositories
    artifact = 'com.google.protobuf:protoc:2.5.0'
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}

clean {
  delete protobuf.generatedFilesBaseDir
  delete "$projectDir/out"
}

// The `generateProto` task is dynamically generated and is not available at configuration time,
// so we need to refer to it by a string name here. The `generateAvroJava` task is available
// at configuration time.
ideaModule.dependsOn 'generateProto', generateAvroJava
idea {
  module {
    // For some reason, you need to add the generated sources dir to *both* sourceDirs and generatedSourceDirs
    // in order for IDEA to mark it properly.
    sourceDirs += generatedSourcesDir
    generatedSourceDirs += generatedSourcesDir
  }
}
