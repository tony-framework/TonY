plugins {
  id "com.commercehub.gradle.plugin.avro" version "0.9.0"
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

dependencies {
  compile project(':tony-mini')

  compile(deps.azkaban.az_core) {
    exclude group: 'com.google.guava'
    exclude group: 'com.google.inject'
  }
  compile(deps.azkaban.az_hadoop_jobtype_plugin) {
    exclude group: 'com.google.guava'
    exclude group: 'com.google.inject'
  }
  compile deps.external.avro
  compile deps.external.jackson_databind
  compile deps.external.py4j
  compile deps.external.text
  compile deps.external.zip4j
  compile deps.hadoop.common
  compile deps.hadoop.hdfs
  compile deps.hadoop.yarn_api
  compile deps.hadoop.yarn_client
  compile deps.hadoop.yarn_common

  testCompile deps.external.avro
  testCompile deps.external.testng
  testCompile deps.external.mockito

  // Only needed by Hadoop test classes
  testRuntime deps.external.junit
}

task setupHdfsLib(type: Copy) {
  from jar, configurations.compile
  exclude '**/*.pom'
  into "$rootDir/tony-core/out/libs/"
}

test {
  workingDir = rootDir
}

test.dependsOn(setupHdfsLib)

def generatedSourcesDir = file('src/generated/main/java')
sourceSets {
  main {
    proto {
      // In addition to the default 'src/main/proto'
      srcDir 'src/main/proto'
      include '**/*.protodevel'
    }

    java {
      srcDirs += [generatedSourcesDir]
    }
  }
}

protobuf {
  // Configure the protoc executable
  protoc {
    // Download from repositories
    artifact = 'com.google.protobuf:protoc:2.5.0'
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}

clean {
  delete protobuf.generatedFilesBaseDir
  delete "$projectDir/out"
}

// The `generateProto` task is dynamically generated and is not available at configuration time,
// so we need to refer to it by a string name here
ideaModule.dependsOn 'generateProto'
idea {
  module {
    // For some reason, you need to add the generated sources dir to *both* sourceDirs and generatedSourceDirs
    // in order for IDEA to mark it properly.
    sourceDirs += generatedSourcesDir
    generatedSourceDirs += generatedSourcesDir
  }
}
